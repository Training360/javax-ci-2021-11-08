plugins {
	id 'org.springframework.boot' version '2.5.6'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
        id 'com.github.ben-manes.versions' version '0.38.0'
        id 'jacoco'
        id 'org.sonarqube' version '3.3'
        id 'maven-publish'
}

group = 'training'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '8'

repositories {
	mavenCentral()
//   maven {
//    url "http://localhost:8091/repository/maven-public/"
//    allowInsecureProtocol true
//  }
}

sourceSets {
    integrationTest {
        java.srcDir "$projectDir/src/integration/java"
        resources.srcDir "$projectDir/src/integration/resources"
        compileClasspath += main.output
        runtimeClasspath += main.output
    }
}

configurations {
    integrationTestImplementation.extendsFrom implementation
    integrationTestRuntimeOnly.extendsFrom runtimeOnly
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.zalando:problem:0.25.0'
    implementation 'org.zalando:jackson-datatype-problem:0.25.0'
    implementation 'org.springdoc:springdoc-openapi-ui:1.5.6'
    implementation 'org.glassfish.jaxb:jaxb-runtime'
    implementation 'org.liquibase:liquibase-core'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    implementation 'org.mapstruct:mapstruct:1.4.2.Final'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.4.2.Final'

    runtimeOnly 'com.h2database:h2'
    runtimeOnly 'org.mariadb.jdbc:mariadb-java-client'

    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'org.mockito:mockito-core'
    testImplementation 'org.mockito:mockito-junit-jupiter'
    testImplementation 'org.assertj:assertj-core'

    integrationTestImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}

    integrationTestImplementation 'io.rest-assured:json-path'
    integrationTestImplementation 'io.rest-assured:json-schema-validator'
    integrationTestImplementation 'io.rest-assured:rest-assured'
    integrationTestImplementation 'io.rest-assured:spring-mock-mvc'

}

test {
	useJUnitPlatform()
        finalizedBy jacocoTestReport
}

jacoco {
    toolVersion = "0.8.7"
}

jacocoTestReport {
   getExecutionData().setFrom(fileTree(buildDir).include("/jacoco/*.exec"))
}


jacocoTestReport {
    reports {
       xml.enabled true
       csv.enabled true
   }

    dependsOn test
}


tasks.register('integrationTest', Test) {
    description = 'Runs integration tests.'
    group = 'verification'

    useJUnitPlatform()

    systemProperty 'spring.datasource.url', findProperty('spring.datasource.url') ?: 'jdbc:h2:mem:db;DB_CLOSE_DELAY=-1'
    systemProperty 'spring.datasource.username', findProperty('spring.datasource.username') ?: 'sa'
    systemProperty 'spring.datasource.password', findProperty('spring.datasource.password') ?: 'sa'

    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    shouldRunAfter test
    finalizedBy jacocoTestReport
}

check.dependsOn integrationTest

sonarqube {
    properties {
        property "sonar.projectName", "employees"
        property "sonar.projectKey", "training:employees"
        property "sonar.jacoco.xmlReportPath", "${project.buildDir}/reports/jacoco/test/jacocoTestReport.xml"
        property "sonar.login", "admin"
        property "sonar.password", "admin12"
    }
}

publishing {
    publications {
        bootJava(MavenPublication) {
            artifact bootJar
        }
    }
    repositories {
        maven {
            def nexusUrl = findProperty('nexusUrl') ?: 'http://localhost:8091'
            def releasesRepoUrl = nexusUrl + "/repository/maven-releases/"
            def snapshotsRepoUrl = nexusUrl + "/repository/maven-snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username findProperty('nexusUsername') ?: 'admin'
                password findProperty('nexusPassword') ?: 'admin12'
            }
            allowInsecureProtocol true
        }
    }
}